#
# A theme based on skwp and steeef theme
#  * RVM/Rbenv info shown on the right
#  * Git branch info on the left
#  * Single line prompt
#  * Works with terminal vim
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Yan Pritzker <yan@pritzker.ws>
#   Ryan Schlesinger <ryan@instanceinc.com>

function prompt_ryansch_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info on
    git-info
  fi
}

function prompt_ryansch_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_ryansch_precmd

  __PROMPT_RYANSCH_COLORS=(
    "%F{cyan}"
    "%F{yellow}"
    "%F{magenta}"
    "%F{red}"
    "%F{green}"
  )

  # git
  zstyle ':prezto:module:git:info:branch' format "${__PROMPT_RYANSCH_COLORS[1]}%b%f"
  zstyle ':prezto:module:git:info:added' format "${__PROMPT_RYANSCH_COLORS[5]}●%f"
  zstyle ':prezto:module:git:info:deleted' format "${__PROMPT_RYANSCH_COLORS[2]}●%f"
  zstyle ':prezto:module:git:info:modified' format "${__PROMPT_RYANSCH_COLORS[4]}●%f"
  zstyle ':prezto:module:git:info:untracked' format "${__PROMPT_RYANSCH_COLORS[3]}●%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '(%b%d%a%m%u)'

  PROMPT="${__PROMPT_RYANSCH_COLORS[3]}%n%f@${__PROMPT_RYANSCH_COLORS[2]}%m%f ${__PROMPT_RYANSCH_COLORS[5]}%~%f "'$git_info[prompt]'"$ "
  RPROMPT=''
}

prompt_ryansch_setup "$@"
